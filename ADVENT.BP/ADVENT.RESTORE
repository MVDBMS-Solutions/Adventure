!
* ADVENTURE game - restore subroutine
*
* reworked and revised for CDI 4/85 JD Ruggiero
*
    PRECISION 0
    COMMON ADVARSE(2090)      ;* overlays entire common area

    OPEN 'ADVENT.MASTER,SAVED.ADVENTURES' TO SAVED ELSE CALL ADVFATAL(1)
    OPEN 'ADVENT.MASTER' TO ADVENTURE ELSE CALL ADVFATAL(2)
    PROMPT ':'

    LOOP
        CRT
        CRT "Enter name of saved game, <return> to start a new game, or X to exit":
        INPUT ID

        BEGIN CASE
        CASE OCONV(ID,'MCU') = 'X'
            STOP

        CASE ID = ''
            CRT @(-1):@(0,3):"Initializing a new game - please wait"
            ENTER ADVENT.INIT

        CASE 1
            MATREAD ADVARSE FROM SAVED,OCONV(ID,'MCU') THEN
                CRT @(-1):@(0,3):"Now restoring - please wait"

* apparently we want here to convert all string numerics into internal direct numbers for speed...
                FOR K = 1 TO 2090
                    IF NUM(ADVARSE(K)) THEN ADVARSE(K)+=0
                NEXT K

                READ ADVARSE(1) FROM ADVENTURE,'5' ELSE STOP          ;* ptext
                READ ADVARSE(2) FROM ADVENTURE,'10' ELSE STOP         ;* ctext
                ADVARSE(3)='RESUME'     ;* parm
                ADVARSE(4)=ADVENTURE    ;* data file
                CRT "Sucessful restore of ":ID:" complete."
                CALL ADVENTURES

            END ELSE
                CRT "No game with that name seems to be saved."
            END
        END CASE
    REPEAT
